agent_of_deleting_sc_element_from_draft
=> nrel_main_idtf:
	[агентная scp-программа удаления sc-элемента из черновика] (* <- lang_ru;; *);
	[agent scp-program of deleting sc-element from draft] (* <- lang_en;; *);
	<- agent_scp_program;;

scp_program -> agent_of_deleting_sc_element_from_draft
(*
	-> rrel_params: .agent_of_deleting_sc_element_from_draft_params 
	(*
		-> rrel_1: rrel_in: _event;;
		-> rrel_2: rrel_in: _input_arc;;
	*);;

	-> rrel_operators: .agent_of_deleting_sc_element_from_draft_operator_set 
	(*
		-> rrel_init: .agent_of_deleting_sc_element_from_draft_operator1
		(*
			<- searchElStr3;;
			-> rrel_1: rrel_assign: rrel_scp_var: _temp;;
			-> rrel_2: rrel_fixed: rrel_scp_var: _input_arc;;
			-> rrel_3: rrel_assign: rrel_scp_var: _quest;;

			=> nrel_goto: .agent_of_deleting_sc_element_from_draft_operator2;;
		*);;
                 
		-> .agent_of_deleting_sc_element_from_draft_operator2
		(*
			<- searchElStr3;;
			-> rrel_1: rrel_fixed: rrel_scp_const: action_of_deleting_sc_elements_from_draft;;
			-> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc1;;
			-> rrel_3: rrel_fixed: rrel_scp_var: _quest;;

			=> nrel_then: .agent_of_deleting_sc_element_from_draft_operator3;;
			=> nrel_else: .agent_of_deleting_sc_element_from_draft_operator_return;;
		*);;

		-> .agent_of_deleting_sc_element_from_draft_operator3
		(*
			<- searchElStr5;;
			-> rrel_1: rrel_fixed: rrel_scp_var: _quest;;
			-> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc1;;
			-> rrel_3: rrel_assign: rrel_scp_var: _sc_element;;
			-> rrel_4: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc2;;
			-> rrel_5: rrel_fixed: rrel_scp_const: rrel_1;;

			=> nrel_then: .agent_of_deleting_sc_element_from_draft_operator4;;
			=> nrel_else: .agent_of_deleting_sc_element_from_draft_operator_call_proc_of_adding_validating_message;;
		*);;

		-> .agent_of_deleting_sc_element_from_draft_operator4
		(*
			<- searchElStr5;;
			-> rrel_1: rrel_fixed: rrel_scp_var: _quest;;
			-> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc1;;
			-> rrel_3: rrel_assign: rrel_scp_var: _draft;;
			-> rrel_4: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc2;;
			-> rrel_5: rrel_fixed: rrel_scp_const: rrel_2;;

			=> nrel_then: .agent_of_deleting_sc_element_from_draft_operator5;;
			=> nrel_else: .agent_of_deleting_sc_element_from_draft_operator_call_proc_of_adding_validating_message;;
		*);;

		-> .agent_of_deleting_sc_element_from_draft_operator5
		(*
		  	<- call;;
		  	-> rrel_1: rrel_fixed: rrel_scp_const: proc_delete_sc_element_from_draft;;
		  	-> rrel_2: rrel_fixed: rrel_scp_const: .agent_of_deleting_sc_element_from_draft_operator_call_proc_delete_sc_element_from_draft
			(*
				-> rrel_1: rrel_fixed: rrel_scp_var: _sc_element;;
				-> rrel_2: rrel_fixed: rrel_scp_var: _draft;;
			*);;
		  	-> rrel_3: rrel_assign: rrel_scp_var: _descr1;;

		  	=> nrel_goto: .agent_of_deleting_sc_element_from_draft_operator6;;
		*);;

		-> .agent_of_deleting_sc_element_from_draft_operator6
		(*
		  	<- waitReturn;;
		  	-> rrel_1: rrel_fixed: rrel_scp_var: _descr1;;

		  	=> nrel_goto: .agent_of_deleting_sc_element_from_draft_operator_make_answer;;
		*);;

		-> .agent_of_deleting_sc_element_from_draft_operator_make_answer
		(*
			<- genElStr5;;
			-> rrel_1: rrel_fixed: rrel_scp_var: _quest;;
			-> rrel_2: rrel_assign: rrel_common: rrel_const: rrel_scp_var: _arc1;;
			-> rrel_3: rrel_fixed: rrel_scp_var: _draft;;
			-> rrel_4: rrel_assign: rrel_pos_const_perm: rrel_const: rrel_scp_var: _arc2;;
			-> rrel_5: rrel_fixed: rrel_scp_const: nrel_answer;; 
			
			=> nrel_goto: .agent_of_deleting_sc_element_from_draft_operator_successfully_finished_question;;
		*);;

		-> .agent_of_deleting_sc_element_from_draft_operator_successfully_finished_question
		(*
			<- genElStr3;;
			-> rrel_1: rrel_fixed: rrel_scp_const: question_finished_successfully;;
			-> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc1;;
			-> rrel_3: rrel_fixed: rrel_scp_var: _quest;;

			=> nrel_goto: .agent_of_deleting_sc_element_from_draft_operator_return;; 
		*);;

/**************************************************************************Wrong Parametrs************************************************************/

		-> .agent_of_deleting_sc_element_from_draft_operator_call_proc_of_adding_validating_message
		(*
		  	<- call;;
		  	-> rrel_1: rrel_fixed: rrel_scp_const: proc_of_adding_validating_message;;
		  	-> rrel_2: rrel_fixed: rrel_scp_const: .agent_of_deleting_sc_element_from_draft_operator_call_proc_of_adding_validating_message_params
			(*
				-> rrel_1: rrel_fixed: rrel_scp_var: _quest;;
				-> rrel_2: rrel_fixed: rrel_scp_const: [Wrong parameter!It must be sc-element and draft!];;
			*);;
		  	-> rrel_3: rrel_assign: rrel_scp_var: _descr1;;

		  	=> nrel_goto: .agent_of_deleting_sc_element_from_draft_operator_waitreturn_proc_of_adding_validating_message;;
		*);;

		-> .agent_of_deleting_sc_element_from_draft_operator_waitreturn_proc_of_adding_validating_message
		(*
		  	<- waitReturn;;
		  	-> rrel_1: rrel_fixed: rrel_scp_var: _descr1;;

		  	=> nrel_goto: .agent_of_deleting_sc_element_from_draft_operator_unsuccessfully_finished_question;;
		*);;

		-> .agent_of_deleting_sc_element_from_draft_operator_unsuccessfully_finished_question
		(*
			<- genElStr3;;
			-> rrel_1: rrel_fixed: rrel_scp_const: question_finished_unsuccessfully;;
			-> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc1;;
			-> rrel_3: rrel_fixed: rrel_scp_var: _quest;;

			=> nrel_goto: .agent_of_deleting_sc_element_from_draft_operator_return;; 
		*);;
		
		-> .agent_of_deleting_sc_element_from_draft_operator_return(* <- return;;*);;
	*);;
*);;
